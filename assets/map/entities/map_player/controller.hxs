var moving:Bool;
var isRunning:Bool;
var movePosition:Point;
var initialPosition:Point;

// Used to shake the main sprite since otherwise the asset-sprite anim offsets override the shake anim offsets
var shakeMainSprite:AssetSprite;

// Component caches
var movement:GameScript;

function onLoaded()
{
	movement = getComponent("movement");

	shakeMainSprite = new AssetSprite(this.x, this.y, null, "entities/player/sprites/main");
	shakeMainSprite.update();
	shakeMainSprite.animation.stop();
	shakeMainSprite.animation.name = null;
	shakeMainSprite.visible = false;
	this.add(shakeMainSprite);

	this.add(new AssetSprite(this.x, this.y, null, "entities/player/sprites/hands"));
}

function onUpdate(elapsed:Float)
{
	if (!moving)
		return;

	var position = new Point(this.x, this.y);
	var angle:Float = position.radiansTo(movePosition);
	var direction:Point = new Point(FlxMath.fastCos(angle), FlxMath.fastSin(angle));
	movement.call("move", [direction, isRunning, elapsed]);
	// If the entity has moved beyond the target position
	if (position.dist(initialPosition) > movePosition.dist(initialPosition))
	{
		moving = false;
		this.x = movePosition.getX();
		this.y = movePosition.getY();
		movement.call("move", [new Point(0.0, 0.0), false, 0.0]); // Stop moving
	}
}

function moveTo(position:Point, running:Bool)
{
	FlxTween.cancelTweensOf(shakeMainSprite);
	this.mainSprite.visible = true;
	shakeMainSprite.visible = false;

	moving = true;
	isRunning = running;
	movePosition = position;
	initialPosition = new Point(this.x, this.y);
}

// Returns true if currently moving to a point
function getMoving():Bool
{
	return moving;
}

// Plays a quick shake animation. Used mainly for map error messages
function shake()
{
	if (moving)
		return;
	this.mainSprite.visible = false;
	shakeMainSprite.visible = true;
	FlxTween.shake(shakeMainSprite, 0.1, 0.25, 1, {onComplete: function()
	{
		this.mainSprite.visible = true;
		shakeMainSprite.visible = false;
	}});
}
