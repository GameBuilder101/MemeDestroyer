// Attack variables
var attackCooldown:Float = 3.0;
var currentAttackIndex:Int = -1;
var currentAttackTime:Float = 0.0;

// Spin attack variables
var spinDir:Point;
var spinSound:AssetSound;
var bounceSound:AssetSound;

// Shoot attack variables
var shootCount:Int;
var shootSound:AssetSound;

// Component caches
var movement:GameScript;
var shooter:GameScript;
var baseAI:GameScript;

function onLoaded()
{
	movement = getComponent("movement");
	shooter = getComponent("shooter");
	baseAI = getComponent("base_ai");

	spinSound = AssetSoundRegistry.getAsset("entities/maxwell/sounds/spin");
	bounceSound = AssetSoundRegistry.getAsset("entities/maxwell/sounds/bounce");

	shootSound = AssetSoundRegistry.getAsset("entities/maxwell/sounds/shoot");
}

function onUpdate(elapsed:Float)
{
	if (baseAI.call("getTarget") == null)
	{
		baseAI.call("setTarget", [state.getEntity("player")]);
		return;
	}

	// Non-attack movement
	if (currentAttackTime <= 0.0)
		movement.call("move", [baseAI.call("getFacingVector"), false, elapsed]);

	// If performing the given attack
	if (currentAttackTime > 0.0)
	{
		switch (currentAttackIndex)
		{
			default:
				updateSpinAttack(elapsed);
				return;
			case 1:
				updateShootAttack(elapsed);
				return;
		}
	}

	// Attacking
	attackCooldown -= elapsed;
	if (attackCooldown <= 0.0)
	{
		attackCooldown = 3.0;

		currentAttackIndex++;
		if (currentAttackIndex > 1)
			currentAttackIndex = 0;
		switch (currentAttackIndex)
		{
			default:
				startSpinAttack();
			case 1:
				startShootAttack();
		}
	}
}

function startSpinAttack()
{
	currentAttackTime = 4.0;

	// Stop moving
	movement.call("move", [new Point(0.0, 0.0), false, 0.0]);

	state.spawn("entities/warning_glint_particle", this.x, this.y);
}

function updateSpinAttack(elapsed:Float)
{
	// Give time for the player to prepare before spinning
	if (currentAttackTime > 3.0)
	{
		currentAttackTime -= elapsed;
		if (currentAttackTime <= 3.0)
		{
			currentAttackTime = 3.0;
			spinDir = baseAI.call("getTargetFacingVector");
			spinSound.play();
		}
		return;
	}

	movement.call("move", [spinDir, true, elapsed]);
}

// The spin attack duration is based on how many times the AI bounces
function onTouchedEdge(touchedEdge:Int, elapsed:Float)
{
	if (currentAttackTime < 0.0 || currentAttackIndex != 0)
		return;
	currentAttackTime--;

	// Bounce the vector
	if (touchedEdge <= 1)
		spinDir.point.x *= -1.0;
	else
		spinDir.point.y *= -1.0;

	bounceSound.play();
	FlxG.camera.shake(0.005, 0.1);
}

function startShootAttack()
{
	currentAttackTime = 1.0;

	shootCount = 0;
	// Stop moving
	movement.call("move", [new Point(0.0, 0.0), false, 0.0]);
}

function updateShootAttack(elapsed:Float)
{
	currentAttackTime -= elapsed;

	if ((currentAttackTime <= 0.666 && shootCount <= 0)
		|| (currentAttackTime <= 0.333 && shootCount <= 1)
		|| (currentAttackTime <= 0.0 && shootCount <= 2))
	{
		shootCount++;
		shooter.call("fire", ["projectiles/generic_bullet", baseAI.call("getTargetFacing"), false]);
		shootSound.play();
	}
}
